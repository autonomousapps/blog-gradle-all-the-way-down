//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyImplicitNullArgumentCall
plugins {
  id 'java-gradle-plugin'
  id 'org.jetbrains.kotlin.jvm'
}

dependencies {
  implementation platform('org.jetbrains.kotlin:kotlin-bom')
  implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
}

testing {
  suites {
    test {
      useJUnitJupiter()
      dependencies {
        implementation 'com.google.truth:truth:1.1.3'
      }
    }

    // Create a new test suite
    functionalTest(JvmTestSuite) {
      useJUnitJupiter()
      dependencies {
        // functionalTest test suite depends on the production code in tests
        implementation project
        implementation 'com.google.truth:truth:1.1.3'
      }

      targets {
        all {
          testTask.configure { shouldRunAfter(test) }
        }
      }
    }
  }
}

gradlePlugin {
  // Define the plugin
  plugins {
    greeting {
      id = 'mutual.aid.complex-thing'
      implementationClass = 'mutual.aid.GradleAllTheWayDownPlugin'
    }
  }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)

tasks.named('check') {
  dependsOn testing.suites.functionalTest
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(11))
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    freeCompilerArgs += '-Xsam-conversions=class'
  }
}
